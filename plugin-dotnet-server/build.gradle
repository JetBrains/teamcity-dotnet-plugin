plugins {
  id 'io.github.rodm.teamcity-server'
  id 'plugin.conventions'
}

teamcity {
  version = project.teamcityVersion
  allowSnapshotVersions = true
  server {
    descriptor = project.file('teamcity-plugin.xml')
    tokens = [Plugin_Version: project.version]
    files {
      into('kotlin-dsl') {
        from("${rootProject.projectDir}/kotlin-dsl")
      }
      into('ui-schemas') {
        from("${rootProject.projectDir}/ui-schemas")
      }
      into('server/tool-descriptors') {
        from('tool-descriptors')
      }
    }
  }
}

dependencies {
  implementation project(':plugin-dotnet-common')

  provided "org.jetbrains.teamcity.internal:server-tools:${teamcityVersion}"
  provided "org.jetbrains.teamcity.internal:server:${teamcityVersion}"
  testImplementation 'org.jmock:jmock-junit4:2.12.0'
  testImplementation 'io.mockk:mockk:1.14.3'
  testImplementation 'org.jetbrains.kotlin:kotlin-reflect'
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1'
  agent project(path: ':plugin-dotnet-agent', configuration: 'plugin')
}

class PrepareToolDescriptorsTask extends DefaultTask {
  @TaskAction
  def run() {
    def toolDescriptorsDir = './tool-descriptors'
    ant.delete(dir: toolDescriptorsDir)
    ant.mkdir(dir: toolDescriptorsDir)
    ant.copy(todir: toolDescriptorsDir, overwrite: 'true', force: 'true'){
      fileset(file: "./dotcover-teamcity-plugin.xml")
      fileset(file: "./resharper-teamcity-plugin.xml")
    }
  }
}

tasks.register('prepareToolDescriptorsTask', PrepareToolDescriptorsTask)

serverPlugin.dependsOn(prepareToolDescriptorsTask)
serverPlugin.archiveFileName = "dotnet.zip"