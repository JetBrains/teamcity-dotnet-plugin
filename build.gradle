/*
 * Copyright 2000-2022 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
}

repositories {
    mavenCentral()
}

ext {
    def correctVersion = project.hasProperty('versionNumber') && property('versionNumber') ==~ /\d+(\.\d+\.\d+.*)?/
    versionNumber = correctVersion ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    projectIds = ['group': 'teamcity-dotnet-plugin', 'version': versionNumber, 'artifact': 'dotnet']
    teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '2021.1'
    teamcityDir = project.findProperty('teamcityDir') ?: "$rootDir/servers/TeamCity-${teamcityVersion}"
    if (teamcityDir == null || !file(teamcityDir).isDirectory()) {
        throw new InvalidUserDataException('Please, execute gradle task `:plugin-dotnet-server:installTeamCity` ' +
                'or specify correct `teamcityDir` path in gradle.properties file')
    }
    teamcityDir = teamcityDir.replace("\\", "/")
    if (teamcityDir.endsWith("/")) {
        teamcityDir = teamcityDir.substring(0, teamcityDir.length() - 5)
    }
    println "TeamCity directory: " + teamcityDir
}

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: "kotlin"

    repositories {
        mavenCentral()
        mavenLocal()
    }

    test.useTestNG()
    jar.version = null
}