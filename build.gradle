plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.51"
}

ext {
    def correctVersion = project.hasProperty('versionNumber') && property('versionNumber') ==~ /\d+\.\d+\.\d+.*/
    versionNumber = correctVersion ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    projectIds = ['group': 'teamcity-dotnet-plugin', 'version': versionNumber, 'artifact': 'dotnet-core-plugin']
    teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '10.0'
}

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: "kotlin"
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    kotlin { experimental { coroutines 'enable' } }

    test.useTestNG()
    jar.version = null
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}